plugins {
    id 'application'
}

application {
    applicationName = 'partiql' // startup script name
    mainClass.set("org.partiql.cli.Main")
}

distributions {
    main {
        distributionBaseName.set('partiql-cli') // archive name
        contents {
            from 'archive'  // include contents of archive dir into the final zip/tar
        }
    }
}

(tasks.findByName("distTar") as Tar).compression = Compression.GZIP

tasks['run'].standardInput = System.in

dependencies {
    implementation project(":lang")
    implementation project(":extensions")
    implementation project(":partiql-spi")

    implementation 'net.sf.jopt-simple:jopt-simple:[5.0,6.0)'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // REPL / Shell
    implementation 'org.jline:jline:3.21.0'
    implementation 'org.fusesource.jansi:jansi:2.4.0'
    implementation 'joda-time:joda-time:2.10.14'
    implementation 'com.google.guava:guava:31.1-jre'

    implementation 'org.apache.commons:commons-csv:1.8'

    testImplementation 'org.junit.vintage:junit-vintage-engine:5.7.0'
}

// COW HACK. We should considering a custom distributions Gradle plugin
task install(type: GradleBuild) {
    tasks = ['assembleDist', 'distZip', 'installDist']
    doLast {
        var generated = new File("$buildDir/install/partiql-cli/bin/partiql")
        var buffer = ""
        generated.readLines().forEach({
            buffer += it + '\n'
            if (it.startsWith("CLASSPATH=")) {
                buffer += """
                    # Add all plugins to classpath
                    for plugin in \$HOME/.partiql/plugins/*.jar; do
                        CLASSPATH=\$CLASSPATH:\$plugin
                    done
                """.stripIndent()
            }
        })
        generated.text = buffer
    }
}
